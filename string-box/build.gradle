apply plugin: 'java-library'
apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'


//任务
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

// pom配置
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "lotty"
            name "lotty-tj"
            email "wuh24.tj@gmail.com"
        }
    }

    scm {
        url "https://github.com/lotty520/box"
    }
}

def bintrayUser = rootProject.properties.get("bintray.user")
def bintrayKey = rootProject.properties.get("bintray.apikey")
def libVersion = rootProject.properties.get("lib.version")

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['stringBox']
    pkg {
        repo = 'maven'
        name = 'box'
        userOrg = 'lotty-tj'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/lotty520/box.git'
        publish = true
        version {
            name = libVersion
            desc = 'java aop class inject library'
            released = new Date()
        }
    }
}

publishing {
    publications {
        stringBox(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.box'
            artifactId 'string'
            version libVersion
            pom.withXml {
                def root = asNode()
                root.appendNode('url', 'https://github.com/lotty520/box.git')
                root.children().last() + pomConfig
            }
        }
    }
}


